Imports System.Windows.Forms

Public Class CalendarColumn
    Inherits DataGridViewColumn

    Public Sub New()
        MyBase.New(New CalendarCell())
    End Sub

    Public Overrides Property CellTemplate As DataGridViewCell
        Get
            Return MyBase.CellTemplate
        End Get
        Set(value As DataGridViewCell)
            If (value IsNot Nothing) AndAlso Not TypeOf (value) Is CalendarCell Then
                Throw New InvalidCastException("Must be a CalendarCell")
            End If
            MyBase.CellTemplate = value
        End Set
    End Property
End Class

Public Class CalendarCell
    Inherits DataGridViewTextBoxCell

    Private Shared calendar As MonthCalendar
    Private Shared currentColumnIndex As Integer
    Private Shared currentRowIndex As Integer

    Shared Sub New()
        calendar = New MonthCalendar()
        calendar.MaxSelectionCount = 1
        calendar.Visible = False

        AddHandler calendar.DateSelected, Sub(sender, e)
                                              Dim grid = DirectCast(calendar.Tag, DataGridView)
                                              grid.Rows(currentRowIndex).Cells(currentColumnIndex).Value =
                                                  e.Start.ToString("yyyy-MM-dd")
                                              calendar.Visible = False
                                          End Sub
    End Sub

    Protected Overrides Sub OnDoubleClick(e As DataGridViewCellEventArgs)
        MyBase.OnDoubleClick(e)

        Dim grid = Me.DataGridView
        If grid Is Nothing Then Return

        If Not calendar.Parent Is grid.Parent Then
            grid.Parent.Controls.Add(calendar)
        End If

        currentColumnIndex = e.ColumnIndex
        currentRowIndex = e.RowIndex
        calendar.Tag = grid

        Dim rect = grid.GetCellDisplayRectangle(e.ColumnIndex, e.RowIndex, True)
        Dim location = grid.Parent.PointToClient(grid.PointToScreen(New Point(rect.Left, rect.Bottom)))

        calendar.Location = location
        calendar.BringToFront()
        calendar.Visible = True
    End Sub
End Class
